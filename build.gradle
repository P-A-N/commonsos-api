apply plugin: 'idea'
apply plugin: 'java'

repositories {
  mavenCentral()
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
  main {
    java { srcDir "src" }
    resources { srcDirs "resources" }
  }
  test {
    java { srcDir "test" }
    resources { srcDirs "test" }
  }
}

dependencies {
  compile ('com.sparkjava:spark-core:2.7.1') {
    exclude group: 'org.slf4j'
  }
  compile 'com.google.code.gson:gson:2.8.2'
  compile 'org.projectlombok:lombok:1.16.18'
  compile 'com.google.inject:guice:4.2.0'
  compile 'ch.qos.logback:logback-classic:1.2.3'
  compile 'org.codehaus.janino:janino:3.0.6'

  compile 'org.hibernate:hibernate-core:5.2.16.Final'
  compile 'org.hibernate:hibernate-c3p0:5.2.16.Final'
  compile 'org.flywaydb:flyway-core:5.0.7'
  compile 'org.postgresql:postgresql:42.2.2'

  compile 'org.web3j:core:3.4.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.15.0'
  testCompile 'org.assertj:assertj-core:3.9.1'
  testCompile 'com.h2database:h2:1.4.197'
}

jar {
  archiveName "commonsos-api.jar"
  manifest {
    attributes 'Main-Class': 'commonsos.Server', 'Application-Version': "$System.env.GIT_REVISION", 'Class-Path': configurations.compile.collect {it.getName()}.join(' ')
  }
}

task copyCompileDependencies(type: Sync) {
  from configurations.compile
  into "build/libs"
}

task bundle(type: Zip, dependsOn: [jar, copyCompileDependencies]) {
  archiveName "commonsos-api.zip"
  from new File(buildDir, "libs")
  from ('start.sh')
  from ('stop.sh')
  into "commonsos-api"
}